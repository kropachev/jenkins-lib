package ru.pulsar.jenkins.library.steps

import com.cloudbees.groovy.cps.NonCPS
import com.fasterxml.jackson.databind.ObjectMapper
import hudson.model.Result
import hudson.scm.ChangeLogSet
import io.jenkins.blueocean.rest.impl.pipeline.FlowNodeWrapper
import io.jenkins.blueocean.rest.impl.pipeline.PipelineNodeGraphVisitor
import io.jenkins.blueocean.rest.model.BlueRun
import io.jenkins.cli.shaded.org.apache.commons.lang.time.DurationFormatUtils
import jenkins.plugins.http_request.HttpMode
import jenkins.plugins.http_request.MimeType
import org.jenkinsci.plugins.workflow.actions.TimingAction
import org.jenkinsci.plugins.workflow.graph.BlockStartNode
import org.jenkinsci.plugins.workflow.job.WorkflowRun
import org.jenkinsci.plugins.workflow.support.steps.build.RunWrapper
import ru.pulsar.jenkins.library.IStepExecutor
import ru.pulsar.jenkins.library.configuration.JobConfiguration
import ru.pulsar.jenkins.library.configuration.Secrets
import ru.pulsar.jenkins.library.ioc.ContextRegistry
import ru.pulsar.jenkins.library.utils.Logger
import ru.pulsar.jenkins.library.utils.RepoUtils
import ru.pulsar.jenkins.library.utils.StringJoiner

import static ru.pulsar.jenkins.library.configuration.Secrets.UNKNOWN_ID

class TelegramNotification implements Serializable {

    private final JobConfiguration config;

    TelegramNotification(JobConfiguration config) {
        this.config = config
    }

    def run() {

        Logger.printLocation()

        if (!config.stageFlags.telegram) {
            Logger.println("Telegram notifications are disabled")
            return
        }

        IStepExecutor steps = ContextRegistry.getContext().getStepExecutor()

        def options = config.notificationsOptions.telegramNotificationOptions

        def currentBuild = steps.currentBuild()
        def currentResult = Result.fromString(currentBuild.getCurrentResult())

        String message = getMessage(currentBuild)

        if (options.onAlways) {
            sendMessage(message)
        } else if (options.onFailure && (currentResult == Result.FAILURE || currentResult == Result.ABORTED)) {
            sendMessage(message)
        } else if (options.onUnstable && currentResult == Result.UNSTABLE) {
            sendMessage(message)
        } else if (options.onSuccess && currentResult == Result.SUCCESS) {
            sendMessage(message)
        } else {
            Logger.println("Unknown build result! Can't send a message to telegram")
        }

    }

    private void sendMessage(message) {
        IStepExecutor steps = ContextRegistry.getContext().getStepExecutor()
        def env = steps.env();

        String repoSlug = RepoUtils.getRepoSlug()

        Secrets secrets = config.secrets

        String telegramChatIdCredentials = secrets.telegramChatId == UNKNOWN_ID ? repoSlug + "_TELEGRAM_CHAT_ID" : secrets.telegramChatId
        String telegramBotTokenCredentials = secrets.telegramBotToken == UNKNOWN_ID ? "TELEGRAM_BOT_TOKEN" : secrets.telegramBotToken

        steps.withCredentials([
            steps.string(telegramBotTokenCredentials, 'TOKEN'),
            steps.string(telegramChatIdCredentials, 'CHAT_ID')
        ]) {

            def mapper = new ObjectMapper()

            def body = [
                chat_id                 : env.CHAT_ID,
                text                    : message,
                disable_web_page_preview: true,
                parse_mode              : 'MarkdownV2'
            ]

            def bodyString = mapper.writeValueAsString(body)
            String url = "https://api.telegram.org/bot${env.TOKEN}/sendMessage"

            steps.echo(message)
            steps.echo(bodyString)

            steps.httpRequest(
                url,
                HttpMode.POST,
                MimeType.APPLICATION_JSON_UTF8,
                bodyString,
                '200',
                true
            )
        }
    }

    private static String getMessage(RunWrapper currentBuild) {

        IStepExecutor steps = ContextRegistry.getContext().getStepExecutor()
        def env = steps.env();

        def currentResult = Result.fromString(currentBuild.getCurrentResult())

        def messageJoiner = new StringJoiner('\n\n')

        def displayName = escapeStringForMarkdownV2(currentBuild.fullDisplayName)
        String header = "[$displayName]($env.BUILD_URL)"
        messageJoiner.add(header)

        String result = ""
        if (currentResult == Result.SUCCESS) {
            result = "âœ… Ð¡Ð±Ð¾Ñ€ÐºÐ° Ð¿Ñ€Ð¾ÑˆÐ»Ð° ÑƒÑÐ¿ÐµÑˆÐ½Ð¾!"
        } else if (currentResult == Result.FAILURE) {
            result = "âŒ Ð¡Ð±Ð¾Ñ€ÐºÐ° Ð·Ð°Ð²ÐµÑ€ÑˆÐ¸Ð»Ð°ÑÑŒ Ñ Ð¾ÑˆÐ¸Ð±ÐºÐ¾Ð¹!"
        } else if (currentResult == Result.ABORTED) {
            result = "ðŸ›‘ Ð¡Ð±Ð¾Ñ€ÐºÐ° Ð¿Ñ€ÐµÑ€Ð²Ð°Ð½Ð°!"
        } else if (currentResult == Result.UNSTABLE) {
            result = "ðŸ’© Ð•ÑÑ‚ÑŒ ÑƒÐ¿Ð°Ð²ÑˆÐ¸Ðµ Ñ‚ÐµÑÑ‚Ñ‹!"
        }

        result = escapeStringForMarkdownV2(result)
        messageJoiner.add(result)

        String stageResults = getStageResultsMessage(currentBuild)
        stageResults = escapeStringForMarkdownV2(stageResults)
        messageJoiner.add(stageResults)

        def duration = "Ð”Ð»Ð¸Ñ‚ÐµÐ»ÑŒÐ½Ð¾ÑÑ‚ÑŒ ÑÐ±Ð¾Ñ€ÐºÐ¸: ${currentBuild.getDurationString()}".replace(" and counting", "")
        duration = escapeStringForMarkdownV2(duration)
        messageJoiner.add(duration)

//        def changeSet = getChangeSet(currentBuild)
//        if (changeSet.length() > 0) {
//            messageJoiner.add('Ð˜Ð·Ð¼ÐµÐ½ÐµÐ½Ð¸Ñ Ñ Ð¿Ð¾ÑÐ»ÐµÐ´Ð½ÐµÐ¹ ÑÐ±Ð¾Ñ€ÐºÐ¸:')
//            messageJoiner.add(changeSet)
//        }

        String buildUrl = "[Ð›Ð¾Ð³ ÑÐ±Ð¾Ñ€ÐºÐ¸](${env.BUILD_URL}console)"
        messageJoiner.add(buildUrl)

        steps.echo(messageJoiner.toString())

        return messageJoiner.toString()
    }

    @NonCPS
    private static String getChangeSet(RunWrapper currentBuild) {
        def changeSetJoiner = new StringJoiner('\n\n')
        currentBuild.changeSets.each {
            it.items.each { item ->
                def entry = (ChangeLogSet.Entry) item
                changeSetJoiner.add(entry.getMsgAnnotated())
            }
        }

        return changeSetJoiner.toString()
    }

    @NonCPS
    private static String getStageResultsMessage(RunWrapper currentBuild) {
        def visitor = new PipelineNodeGraphVisitor(currentBuild.rawBuild as WorkflowRun)
        def stages = visitor.pipelineNodes.findAll { it.type != FlowNodeWrapper.NodeType.STEP }

        def stageResultMessage = ""
        for (FlowNodeWrapper stage in stages) {
            if (stage.status.result == BlueRun.BlueRunResult.SUCCESS || stage.status.result == BlueRun.BlueRunResult.NOT_BUILT) {
                continue
            }


            long duration
            def endNode = stage.node.getExecution().getEndNode(stage.node as BlockStartNode)
            if (endNode != null) {
                def startTime = TimingAction.getStartTime(stage.node)
                def endTime = TimingAction.getStartTime(endNode)

                duration = endTime - startTime
            } else {
                duration = stage.timing.totalDurationMillis
            }

            def time = DurationFormatUtils.formatDuration(duration, "H:mm:ss")
            stageResultMessage += "$stage.displayName: $stage.status.result, Ð·Ð°Ñ‚Ñ€Ð°Ñ‡ÐµÐ½Ð¾ Ð²Ñ€ÐµÐ¼ÐµÐ½Ð¸ $time  \n"
        }

        return stageResultMessage.trim()
    }

    private static String escapeStringForMarkdownV2(String incoming) {
        return incoming.replace('#', "\\#")
            .replace('!', "\\!")
            .replace('.', "\\.")
            .replace('=', "\\=")
            .replace('{', "\\{")
            .replace('}', "\\}")
    }
}
